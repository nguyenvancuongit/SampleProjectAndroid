apply plugin: 'com.android.application'
apply plugin: 'android-apt'
apply plugin: 'me.tatarka.retrolambda'

buildscript {
    repositories {
        mavenCentral()
    }
    dependencies {
        classpath 'com.neenbedankt.gradle.plugins:android-apt:1.8'
        classpath 'me.tatarka:gradle-retrolambda:3.2.5'
    }
}

repositories {
    mavenCentral()
}

android {
    compileSdkVersion 24
    buildToolsVersion "24.0.0"

    defaultConfig {
        applicationId "android.app.tempapplication"
        minSdkVersion 16
        targetSdkVersion 24
        versionCode 1
        versionName "1.0"
    }
    signingConfigs {
        release {
            try {
                storeFile file("../release/keystore.file")
                storePassword KEYSTORE_PASSWORD
                keyAlias "keystore"
                keyPassword KEY_PASSWORD
            }
            catch (ex) {
                ex.printStackTrace()
                throw new InvalidUserDataException("You should define KEYSTORE_PASSWORD and KEY_PASSWORD in gradle.properties.")
            }
        }
    }
    buildTypes {
        release {
            signingConfig signingConfigs.release
            minifyEnabled true
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
        applicationVariants.all { variant ->
            variant.outputs.each { output ->
                def apkName = "temp_project";
                def formattedDate = new Date().format('yyMMdd')
                if (variant.buildType.name == "release") {
                    apkName += "_" + variant.productFlavors[0].name + "_" + formattedDate + "_v" + android.defaultConfig.versionName + ".apk";
                    output.outputFile = new File("../release", apkName)
                } else {
                    apkName += "_" + variant.productFlavors[0].name + "_" + formattedDate + "_v" + android.defaultConfig.versionName + ".apk";
                    output.outputFile = new File((String) output.outputFile.parent, apkName)
                }
            }
        }
    }
    productFlavors {
        server_test {
            applicationId "android.app.tempapplication.dev"
            buildConfigField "String", "HOST_EC2_HOTESPA", "\"HOST_ADDRESSS_TAGING\""
        }
        server_prod {
            applicationId "android.app.tempapplication"
            buildConfigField "String", "HOST_EC2_HOTESPA", "\"HOST_ADDRESS_PRODUCTION\""
        }
    }
    variantFilter { variant ->
        def names = variant.flavors*.name

        if (names.contains("server_test") && variant.buildType.name == "release") {
            variant.ignore = true
        }
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
}

dependencies {
    compile fileTree(dir: 'libs', include: ['*.jar'])
    testCompile 'junit:junit:4.12'
    compile 'com.android.support:appcompat-v7:24.0.0'
    compile 'com.googlecode.libphonenumber:libphonenumber:7.3.0'
    compile 'com.android.support:design:24.0.0'
    compile 'com.jakewharton:butterknife:8.2.1'
    apt 'com.jakewharton:butterknife-compiler:8.2.1'
}
