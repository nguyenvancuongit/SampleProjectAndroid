apply plugin: 'com.android.application'
apply plugin: 'android-apt'
apply plugin: 'me.tatarka.retrolambda'

buildscript {
    repositories {
        mavenCentral()
    }
    dependencies {
        classpath 'com.neenbedankt.gradle.plugins:android-apt:1.8'
        classpath 'me.tatarka:gradle-retrolambda:3.2.5'
    }
}

repositories {
    mavenCentral()
}

android {
    compileSdkVersion 26
    buildToolsVersion '26.0.1'

    defaultConfig {
        applicationId "com.app.temp"
        minSdkVersion 16
        targetSdkVersion 26
        versionCode 1
        versionName "1.0"
    }
    signingConfigs {
        release {
            try {
                storeFile file("../release/keystore.file")
                storePassword KEYSTORE_PASSWORD
                keyAlias "keystore"
                keyPassword KEY_PASSWORD
            }
            catch (ex) {
                ex.printStackTrace()
                throw new InvalidUserDataException("You should define KEYSTORE_PASSWORD and KEY_PASSWORD in gradle.properties.")
            }
        }
    }
    buildTypes {
        release {
            signingConfig signingConfigs.release
            minifyEnabled true
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt')
        }
        applicationVariants.all { variant ->
            variant.outputs.each { output ->
                def apkName = "temp_project";
                def formattedDate = new Date().format('yyMMdd')
                if (variant.buildType.name == "release") {
                    apkName += "_" + variant.productFlavors[0].name + "_" + formattedDate + "_" + variant.buildType.name + "_v" + android.defaultConfig.versionName + ".apk";
                    output.outputFile = new File(getRootDir().path + "/release", apkName)
                } else {
                    apkName += "_" + variant.productFlavors[0].name + "_" + formattedDate + "_" + variant.buildType.name + "_v" + android.defaultConfig.versionName + ".apk";
                    output.outputFile = new File((String) output.outputFile.parent, apkName)
                }
            }
        }
    }
    productFlavors {
        server_test {
            applicationId "com.app.temp.dev"
            buildConfigField "String", "HOST_EC2_HOTESPA", "\"HOST_ADDRESSS_STAGING\""
        }
        server_prod {
            applicationId "com.app.temp"
            buildConfigField "String", "HOST_EC2_HOTESPA", "\"HOST_ADDRESS_PRODUCTION\""
            proguardFiles 'proguard-rules.pro'
        }
    }
    variantFilter { variant ->
        def names = variant.flavors*.name

        if (names.contains("server_test") && variant.buildType.name == "release") {
            variant.ignore = true
        }
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
}

dependencies {
    compile fileTree(dir: 'libs', include: ['*.jar'])
    testCompile 'junit:junit:4.12'
    compile 'com.android.support:appcompat-v7:26.0.0'
    compile 'com.googlecode.libphonenumber:libphonenumber:7.3.0'
    compile 'com.android.support:design:26.0.0'
    compile 'com.jakewharton:butterknife:8.2.1'
    apt 'com.jakewharton:butterknife-compiler:8.2.1'
}
